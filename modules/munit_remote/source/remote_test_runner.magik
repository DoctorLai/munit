#% text_encoding = iso8859_1
_package sw
$

def_slotted_exemplar(:remote_test_runner,
	{
		{:retry?, _unset}
	},
	{:xml_test_runner})
$


_pragma(classify_level=debug)
_method remote_test_runner.run_remote(address, port)
	## 
	##

	start << date_time_now()
	count << 0
	tests_ok? << _true
	.retry? << _true 

	_loop
		
		(module_name, test_name) << _self.get_new_test(address, port)

		_if module_name _is :done _then _leave _endif

		count +<< 1

		_if _not sw_module_manager.module(module_name).loaded?
		_then 
			sw_module_manager.load_module(module_name)
		_endif

		product_name << sw_module_manager.module(module_name).product.name
		_self.properties[:output_identifier] << product_name
		
		test << !current_package![test_name]
		a_stream << _self.new_stream(test)
		_protect
			new_runner << _self.new(a_stream, _scatter _self.properties.for_scatter())
			new_runner.int!run(test)
			a_stream.flush()
		_protection
			a_stream.close()
		_endprotect
		
		tests_ok? << tests_ok? _andif
			     new_runner _isnt _unset _andif
			     new_runner.test_result _isnt _unset _andif 
			     new_runner.test_result.error_count() = 0 _andif
			     new_runner.test_result.failure_count() = 0
				
	_endloop

	show(write_string("Ran ", count, " tests in ", date_time_now() - start))
	
	>> tests_ok?
	
_endmethod
$


_pragma(classify_level=debug)
_private _method remote_test_runner.connect(address, port)
	## 
	##

	count << 0
	_loop 
		_try
			connection << tcpip_connection.new(address, port)
			.retry? << _false
		_when sw_net_connection_failed
			connection << _unset
			.retry? << .retry? _andif count < 1000
		_endtry
		_if .retry? _isnt _true _then _leave _endif
		_thisthread.sleep(150)
		show("Trying to connect... ", count)
		count +<< 1
	_endloop 

	>> connection
	
_endmethod
$


_pragma(classify_level=debug)
_private _method remote_test_runner.get_new_test(address, port)
	## 
	##

	_protect
		
		connection  << _self.connect(address, port)
		module_name << :done

		_if connection _isnt _unset
		_then
			
			# Get the module name
			module_name << _self.get_name_from_connection(connection)
				
			# Get the test name
			_if module_name _isnt :done
			_then 
				test_name << _self.get_name_from_connection(connection)
			_endif
			
		_endif
			
	_protection
		_self.close_connection(connection)
	_endprotect

	>> (module_name, test_name)
	
_endmethod
$


_pragma(classify_level=debug)
_private _method remote_test_runner.get_name_from_connection(connection)
	## 
	##
	
	nchars << connection.input.get_signed_int()
	_if nchars _isnt _unset
	_then
		name << connection.input.get_ascii_chars(nchars).as_symbol()
	_else
		name << :done
	_endif

	>> name
	
_endmethod
$


_pragma(classify_level=debug)
_private _method remote_test_runner.close_connection(connection)
	## 
	##

	_if connection _isnt _unset
	_then 
		connection.close()
	_endif
	
_endmethod
$
